---
title: Groovy Regular Expressions - The Definitive Guide
date: 2019-03-18 12:46:39
updated: 2020-04-24 23:44:47
categories:
    - Groovy Cookbook
tags:
    - groovy
    - regexp
    - regular-expressions
    - jmh
    - java
    - benchmark
cover: /images/groovy-regexp-jmh-bg.jpg
og_image: /images/og/groovy-regexp-jmh.jpg
eyeCatchImage: /images/og/groovy-regexp-jmh.jpg
affiliates: true
highlightjs: true
---
:toc: macro
:matcher-javadoc: https://docs.oracle.com/en/java/javase/11/docs/api/java.base/java/util/regex/Matcher.html
:multiple-assignment: https://groovy-lang.org/semantics.html#_multiple_assignment
:regexp-benchmark-repo: https://github.com/wololock/groovy-regexp-examples

Welcome to _"Groovy Regular Expressions - The Definitive Guide"_!
In the next 15 minutes, you are going to learn everything you need to start working productively with regular expressions in Groovy programming language.
Let's get started!

++++
<!-- more -->
++++

toc::[]

== Introduction

I always found working with regular expressions in Java kind error-prone.
It happened to me many times that I didn't escape backslash character enough times, or I forgot to call `matcher.matches()` or `matcher.find()` explicitly.
Luckily, the Groovy programming language makes working with regex much simpler.
Let's start by learning a few new operators that drastically improve our experience.

== Operators

=== `~string` (pattern operator)

Groovy makes initializing `java.util.regex.Pattern` class simple thanks to the _pattern operator_.
All you have to do is to put `~` right in front of the string literal (e.g. `~"([Gg]roovy)"`), and it creates `java.util.regex.Pattern` object instead of the `java.lang.String` one.

.Creating pattern object example
[source,groovy]
----
import java.util.regex.Pattern

def pattern = ~"([Gg])roovy"

assert pattern.class == Pattern
----

The above example is an equivalent of the following (more explicit) code:

.More explicit pattern object creation example
[source,groovy]
----
import java.util.regex.Pattern

def pattern = Pattern.compile("([Gg])roovy")

assert pattern.class == Pattern
----

==== The difference between `~"pattern"` and `~/pattern/`
[NOTE]
=====
Groovy offers one significant improvement when it comes to working with regular expressions - so-called _slashy_ strings.
This syntax produces either regular `java.lang.String` (if it has no variables to interpolate) or `groovy.lang.GString` (if it contains variables to interpolate.)

[source,groovy]
----
// Running on Groovy 3.0.3

def number = 2

def str1 = /The number is 2/
def str2 = /The number is $number/

assert str1 instanceof String
assert str2 instanceof GString
----

The most useful feature of _slashy_ string is that it eliminates the need for escaping backslashes in the regular expression.

[source,groovy]
----
// Running on Groovy 3.0.3

assert (/Version \d+\.\d+\.\d+/) == 'Version \\d+\\.\\d+\\.\\d+'
----

Of course, you have to remember to escape `$` if you use one in the regular expression.

[source,groovy]
----
// Running on Groovy 3.0.3

assert 'The price is $99' ==~ /The price is \$\d+/
----
=====

Now when you know how to construct pattern objects in Groovy, let's look at what we can do with it.

=== `=~` (find operator)

To create `java.util.regex.Matcher` object, you can use Groovy's _find_ operator.
On the left side, you put a string you want to test matching on.
On the right side, you put a pattern, that can be either `java.util.regex.Pattern` or `java.lang.String`.
Consider the following example.

.Creating matcher using find operator example.
[source,groovy]
----
// Running on Groovy 3.0.3

def pattern = ~/\S+er\b/
def matcher = "My code is groovier and better when I use Groovy there" =~ pattern

assert pattern instanceof java.util.regex.Pattern
assert matcher instanceof java.util.regex.Matcher

assert matcher.find()
assert matcher.size() == 2
assert matcher[0..-1] == ["groovier", "better"]
----

Creating `java.util.regex.Pattern` object in the above example is optional.
Instead, we could define a pattern using _slashy_ string directly in the matcher line.

[source,groovy]
----
// Running on Groovy 3.0.3

def matcher = "My code is groovier and better when I use Groovy there" =~ /\S+er\b/

assert matcher instanceof java.util.regex.Matcher

assert matcher.find()
assert matcher.size() == 2
assert matcher[0..-1] == ["groovier", "better"]
----

When you get the `java.util.regex.Matcher` object, you can essentially use any of {matcher-javadoc}[its standard methods], or you can continue reading to learn more _Groovy_ way to do it.

==== Using `=~` operator in context of boolean

You can also use `java.util.regex.Matcher` object in the context of the _boolean expression_ (e.g., inside the _if-statement_.)
In this case, Groovy implicitly invokes the `matcher.find()` method, which means that the expression evaluates to `true` if any part of the string matches the pattern.

.Using matcher in the context of boolean expression example.
[source,groovy]
----
// Running on Groovy 3.0.3

if ("My code is groovier and better when I use Groovy there" =~ /\S+er\b/) {
    println "At least one element matches the pattern..."
}

if ("Lorem ipsum dolor sit amet" =~ /\d+/) {
    println "This line is not executed..."
}
----

=== `==~` (exact match operator)

Groovy also adds a very useful `==~` _exact match_ operator.
It can be used in a similar way to the _find_ operator, but it behaves a bit differently.
It does not create `java.util.regex.Matcher` object, and instead, it returns `boolean` value.
You can think of it as an equivalent of `matcher.matches()` method call - it tests if the entire string matches given pattern.

.Using exact match operator examples.
[source,groovy]
----
// Running on Groovy 3.0.3

assert "v3.12.4" ==~ /v\d{1,3}\.\d{1,3}\.\d{1,3}/

assert !("GROOVY-123: some change" ==~ /[A-Z]{3,6}-\d{1,4}/)

assert "GROOVY-123: some change" ==~ /[A-Z]{3,6}-\d{1,4}.{1,100}/
----

== Usage examples

Checking if specific string matches given pattern is not the only thing you can do with regular expressions.
In many cases, you want to extract the data that matches the specific pattern or even replace all occurrences with a new value.
You will learn how you can do such things using Groovy.

include::source/_parts/groovy-in-action.adoc[]

=== Extracting all matching elements

Let's begin with extracting all matching elements.
Groovy adds `findAll()` method to `java.util.regex.Matcher` class, and when invoked, it returns all matching elements.
The below example uses this technique to extract all numbers from the given text.

.Extracting all matching elements example.
[source,groovy]
----
// Running on Groovy 3.0.3

def text = """ //<1>
This text contains some numbers like 1024
or 256. Some of them are odd (like 3) or
even (like 2).
"""

def result = (text =~ /\d+/).findAll()

assert result == ["1024", "256", "3", "2"] //<2>
----
<1> Groovy's multiline string example.
<2> Extracted values are of `java.lang.String` type. You may need to map them to integers if needed.

=== Extracting words that begin and end with the same letter

Let's take a look at some practical more examples.
In some cases, you need to extract words that start and end with the same (case-insensitive) letter.
We could use a pattern `/(?i)\b([a-z])[a-z]*\1\b/`, where:

* `(?i)` makes matching case-insensitive,
* `\b([a-z])` defines a group that matches the first letter in the word,
* `\1` refers to the first group (first letter in the word), and `\b` matches the end of the word.

This pattern extracts both the matching word and the letter.
In Groovy, we can use _spread_ operator to call `first()` method on each element to extract matching words.

.Extracting words that begin and end with the same letter.
[source,groovy]
----
// Running on Groovy 3.0.3

def result = ("This is test. Test is good, lol." =~ /(?i)\b([a-z])[a-z]*\1\b/).findAll()*.first()

assert result == ["test", "Test", "lol"]
----

=== Extracting matching element(s) using named group

Java (and thus Groovy) supports named groups in the regular expressions.
When you group a pattern using parentheses, add `?<name>` right after the opening parenthesis to name a group.
Naming groups allows you to extract values from matching pattern using those names, instead of the numeric index value.
You can also use this named group to refer to the matching value when you call `replaceAll()` method on a matcher object.

In the below example, we use a pattern that defines `?<jira>` named group.

.Extracting matching element(s) using named group example.
[source,groovy]
----
// Running on Groovy 3.0.3

def matcher = "JIRA-231 lorem ipsum dolor sit amet" =~ /^(?<jira>[A-Z]{2,4}-\d{1,3}).*$/

assert matcher.matches() // <1>
assert matcher.group("jira") == "JIRA-231" // <2>
assert matcher.replaceAll('Found ${jira} ID') == 'Found JIRA-231 ID' // <3>
----
<1> You need to test if pattern matches before you can extract group by name.
<2> When the string matches the pattern, you can use `group(name)` method to extract matching group.
<3> We can also use `replaceAll()` method to create a new string. *Make sure you use a single quote String*. Otherwise Groovy will try to interpolate `${jira}` and fail.

=== Using multi assignment to extract matching elements

Another useful feature is {multiple-assignment}[multiple variable assignment].
We can use it to extract matching values and assign them directly to specific variables.
Let's say you are parsing some data containing items with their prices and (optional) discount.
Here is how you can extract price and discount and assign it to a variable in one line.

.Using multiple assignments with a matcher object example.
[source,groovy]
----
// Running on Groovy 3.0.3

def (_,price,discount) = ('Some item name: $99.99 (-15%)' =~ /\$(\d{1,4}\.\d{2})\s?\(?(-\d+%)?\)?/)[0]

assert _ == '$99.99 (-15%)'
assert price == "99.99"
assert discount == "-15%"
----

I used `_` as a name for the first variable that stores matching region, not useful in our case.
Now, what happens if the row we process does not contain any discount information?
The `discount` variable is set to `null`.

.No discount information example.
[source,groovy]
----
// Running on Groovy 3.0.3

def (_,price,discount) = ('Some item name: $49.99' =~ /\$(\d{1,4}\.\d{2})\s?\(?(-\d+%)?\)?/)[0]

assert _ == '$49.99'
assert price == "49.99"
assert discount == null
----

Another popular example is extracting minor, major, and patch parts from the semantic version name.
We can use multiple assignments to extract all three parts in a single line of code.

.Using multiple assignments to extract major, minor, and patch from the semantic version.
[source,groovy]
----
// Running on Groovy 3.0.3

def (_,major,minor,patch) = ("v3.21.0" =~ /^v(\d{1,3})\.(\d{1,3})\.(\d{1,3})$/)[0]

assert _ == "v3.21.0"
assert major == "3"
assert minor == "21"
assert patch == "0"
----

=== Replacing matching elements using `replaceFirst()`

Extracting parts of the semantic version name to specific variables looks good, but what if I want to generate a new version by incrementing the patch part?
Well, there is a simple solution to that problem as well.
Groovy overloads `String.replaceFirst(String rgxp, String replacement)` method with `replaceFirst(Pattern p, Closure c)` and this variant is very powerful.
We can extract matching parts in the closure and modify them as we wish.
Take a look at the following example to see how you can increment the patch part in the semantic version.

.Using `replaceFirst()` to increment patch part of the semantic version.
[source,groovy]
----
// Running on Groovy 3.0.3

def version = "v3.21.0"
def expected = "v3.21.1"
def pattern = ~/^v(\d{1,3})\.(\d{1,3})\.(\d{1,3})$/

def newVersion = version.replaceFirst(pattern) { _, major, minor, patch ->
    "v${major}.${minor}.${(patch as int) + 1}"
}

assert newVersion == expected
----

=== Using pattern matcher in the `switch` case

Groovy extends supported types in the `switch` statement and allows you to use patterns.
In this case, Groovy executes `matcher.find()` method to test if any region of the input string matches the pattern.
Consider the following example.

.Pattern in the switch case example.
[source,groovy]
----
// Running on Groovy 3.0.3

def input = "test"

switch (input) {
    case ~/\d{3}/:
        println "The number has 3 digits."
        break

    case ~/\w{4}/:
        println "The word has 4 letters."
        break

    default:
        println "Unrecognized..."
}
----

Running the above example produces the following output.

[source,text]
----
The word has 4 letters.
----

== Benchmark (optional)

In the last segment of this detailed blog post, I want to show you some benchmarks.
And let me make one thing clear - the following results you are going to see are not scientific proof.
I present those results only to give you a hint about the overall performance of some cool features you have seen before.
You will see that, in general, all presented approaches, and their equivalents perform quite similar, so there is no need to refactor your code after reading this section.
If you never saw JMH benchmark tests, you are going to learn something new in a few minutes.
Let's go!

[NOTE]
====
Here you can find the {regexp-benchmark-repo}[Gradle project] containing all benchmark tests.
Below are steps to execute those tests on your machine.

[source,bash,subs="attributes"]
----
$ git clone {regexp-benchmark-repo}
Cloning into 'groovy-regexp-examples'...
remote: Enumerating objects: 29, done.
remote: Total 29 (delta 0), reused 0 (delta 0), pack-reused 29
Unpacking objects: 100% (29/29), 10.64 KiB | 1.33 MiB/s, done.

$ cd groovy-regexp-examples

$ ./gradlew --no-daemon jmh
----
====

NOTE: Here are the specs of the laptop I run benchmark tests on Lenovo ThinkPad T440p laptop with *Intel(R) Core(TM) i7-4900MQ CPU @ 2.80GHz* and *16 GBs RAM*.

I run those benchmarks with four different Groovy and JDK variants:

* Groovy 2.5.11-indy and OpenJDK 1.8.0_242 (64-Bit Server VM, 25.242-b08)
* Groovy 2.5.11-indy and OpenJDK 11.0.6 (64-Bit Server VM, 11.0.6+10)
* Groovy 3.0.3-indy and OpenJDK 1.8.0_242 (64-Bit Server VM, 25.242-b08)
* Groovy 3.0.3-indy and OpenJDK 11.0.6 (64-Bit Server VM, 11.0.6+10)

Every benchmark test was executed using both dynamic and static compilation.

TIP: All measurements are expressed in μs/op. Remember, that *1 μs* is equal to *0.001 ms* (millisecond) and *0.000001 s* (second).

=== Benchmark 1: multiple assignments from the regular expression

In this benchmark, I wanted to check if using multiple variable assignments comes with a significant cost compared to a traditional group extraction.
Here is what the test class looks like.

[source,groovy]
----
package bench

import groovy.transform.CompileStatic
import org.openjdk.jmh.annotations.Benchmark
import org.openjdk.jmh.annotations.Scope
import org.openjdk.jmh.annotations.State

import java.util.regex.Matcher
import java.util.regex.Pattern

@State(Scope.Benchmark)
class A1_Multiple_Assignment_Bench {

    private static final Random random = new Random()

    private static final List<String> data = [
            'Some item name 1: $99.99 (-15%)',
            'Some item name 2: $49.99 (-5%)',
            'Some item name 3: $19.99',
            'Some item name 4: $29.99 (-13%)',
            'Some item name 5: $9.99',
            'Some item name 6: $51.21 (-2%)',
            'Some item name 7: $4.32 (-1%)',
            'Some item name 8: $3.14 (-23%)',
            'Some item name 9: $9.00'
    ]

    private static final Pattern pattern = ~/\$(\d{1,4}\.\d{2})\s?\(?(-\d+%)?\)?/

    private static final String getInput() {
        return data.get(random.nextInt(data.size()))
    }

    @Benchmark
    String multiple_assignment_dynamic() {
        def (_,price,discount) = (input =~ pattern)[0]

        return "price: $price, discount: $discount"
    }

    @Benchmark
    @CompileStatic
    String multiple_assignment_static() {
        final List list = (input =~ pattern)[0] as ArrayList

        def (_, price, discount) = [list[0], list[1], list[2]]

        return "price: $price, discount: $discount"
    }

    @Benchmark
    String standard_matcher_dynamic() {
        final Matcher matcher = pattern.matcher(input)
        if (!matcher.find()) {
            throw new IllegalStateException("Pattern didn't match!")
        }

        def price = matcher.group(1)
        def discount = matcher.group(2)

        return "price: $price, discount: $discount"
    }

    @Benchmark
    @CompileStatic
    String standard_matcher_static() {
        final Matcher matcher = pattern.matcher(input)
        if (!matcher.find()) {
            throw new IllegalStateException("Pattern didn't match!")
        }

        def price = matcher.group(1)
        def discount = matcher.group(2)

        return "price: $price, discount: $discount"
    }
}
----

Here are the results for specific Groovy and JDK versions.

==== Groovy 2.5.11-indy and OpenJDK 1.8.0_242

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A1_Multiple_Assignment_Bench.multiple_assignment_dynamic  avgt   60  0,808 ± 0,013  us/op
A1_Multiple_Assignment_Bench.multiple_assignment_static   avgt   60  0,688 ± 0,009  us/op
A1_Multiple_Assignment_Bench.standard_matcher_dynamic     avgt   60  0,409 ± 0,007  us/op
A1_Multiple_Assignment_Bench.standard_matcher_static      avgt   60  0,387 ± 0,006  us/op
----

==== Groovy 2.5.11-indy and OpenJDK 11.0.6

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A1_Multiple_Assignment_Bench.multiple_assignment_dynamic  avgt   60  1,087 ± 0,022  us/op
A1_Multiple_Assignment_Bench.multiple_assignment_static   avgt   60  0,967 ± 0,023  us/op
A1_Multiple_Assignment_Bench.standard_matcher_dynamic     avgt   60  0,545 ± 0,012  us/op
A1_Multiple_Assignment_Bench.standard_matcher_static      avgt   60  0,538 ± 0,013  us/op
----

==== Groovy 3.0.3-indy and OpenJDK 1.8.0_242

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A1_Multiple_Assignment_Bench.multiple_assignment_dynamic  avgt   60  0,720 ± 0,012  us/op
A1_Multiple_Assignment_Bench.multiple_assignment_static   avgt   60  0,750 ± 0,018  us/op
A1_Multiple_Assignment_Bench.standard_matcher_dynamic     avgt   60  0,427 ± 0,010  us/op
A1_Multiple_Assignment_Bench.standard_matcher_static      avgt   60  0,371 ± 0,006  us/op
----

==== Groovy 3.0.3-indy and OpenJDK 11.0.6

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A1_Multiple_Assignment_Bench.multiple_assignment_dynamic  avgt   60  0,958 ± 0,018  us/op
A1_Multiple_Assignment_Bench.multiple_assignment_static   avgt   60  0,989 ± 0,018  us/op
A1_Multiple_Assignment_Bench.standard_matcher_dynamic     avgt   60  0,522 ± 0,013  us/op
A1_Multiple_Assignment_Bench.standard_matcher_static      avgt   60  0,514 ± 0,008  us/op
----

And here what those results look like on a single chart.

[.text-center]
--
[.img-responsive.img-thumbnail]
[link=/images/groovy-regexp-chart-01.png]
image::/images/groovy-regexp-chart-01.png[]
--

Thoughts?

* We can see that OpenJDK 11.0.6 is just a bit "slower" compared to OpenJDK 1.8.0_242.
* In most cases, Groovy 3.0.3 performed a bit better compared to other variants.
* It still does not prove anything.
If we compare the fastest multiple assignments in a dynamic compilation variant (`0.720 μs`) with the slowest one (`1.087 μs`), we will find that it was around `0.367 μs` slower.
Or `0.000367 ms` to show it to you on a scale that is much easier to imagine.
Can it be a bottleneck in your application? Absolutely not.

=== Benchmark 2: exact match operator

In this benchmark, we compare the performance of the exact match operator with the traditional `matcher.matches()`.
Here is the test class.

[source,groovy]
----
package bench

import groovy.transform.CompileStatic
import groovy.transform.TypeChecked
import org.openjdk.jmh.annotations.Benchmark
import org.openjdk.jmh.annotations.Scope
import org.openjdk.jmh.annotations.State

import java.util.regex.Matcher
import java.util.regex.Pattern

@State(Scope.Benchmark)
class A2_Exact_Match_Operator_Bench {

    private static final Random random = new Random()

    private static final List<String> data = [
            "1605-FACD-0000-EXIT",
            "1606-FACD-0000-EXIT",
            "1607-FACD-0000-EXIT",
            "1608-FACD-0000-EXIT",
            "1609-FACD-0000-EXIT",
            "1610-FACD-0000-EXIT",
            "1611-FACD-0000-EXIT",
            "1611-FACD-0001-EXIT",
            "1611-FACD-0002-EXIT",
            "1611-FACD-0003-EXIT",
            "1612-FACD-0000-EXIT"
    ]

    private static final Pattern pattern = ~/^\d{4}-[A-Z]{4}-0000-EXIT$/

    private static final String getInput() {
        return data.get(random.nextInt(data.size()))
    }

    @Benchmark
    boolean match_operator_dynamic() {
        return input ==~ pattern
    }

    @Benchmark
    @CompileStatic
    boolean match_operator_static() {
        return input ==~ pattern
    }

    @Benchmark
    boolean matcher_matches_dynamic() {
        final Matcher matcher = pattern.matcher(input)

        return matcher.matches()
    }

    @Benchmark
    @CompileStatic
    boolean matcher_matches_static() {
        final Matcher matcher = pattern.matcher(input)

        return matcher.matches()
    }
}
----

And here are the results.

==== Groovy 2.5.11-indy and OpenJDK 1.8.0_242

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A2_Exact_Match_Operator_Bench.match_operator_dynamic      avgt   60  0,243 ± 0,003  us/op
A2_Exact_Match_Operator_Bench.match_operator_static       avgt   60  0,220 ± 0,005  us/op
A2_Exact_Match_Operator_Bench.matcher_matches_dynamic     avgt   60  0,162 ± 0,003  us/op
A2_Exact_Match_Operator_Bench.matcher_matches_static      avgt   60  0,147 ± 0,003  us/op
----

==== Groovy 2.5.11-indy and OpenJDK 11.0.6

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A2_Exact_Match_Operator_Bench.match_operator_dynamic      avgt   60  0,262 ± 0,004  us/op
A2_Exact_Match_Operator_Bench.match_operator_static       avgt   60  0,285 ± 0,004  us/op
A2_Exact_Match_Operator_Bench.matcher_matches_dynamic     avgt   60  0,243 ± 0,004  us/op
A2_Exact_Match_Operator_Bench.matcher_matches_static      avgt   60  0,162 ± 0,004  us/op
----

==== Groovy 3.0.3-indy and OpenJDK 1.8.0_242

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A2_Exact_Match_Operator_Bench.match_operator_dynamic      avgt   60  0,237 ± 0,004  us/op
A2_Exact_Match_Operator_Bench.match_operator_static       avgt   60  0,227 ± 0,003  us/op
A2_Exact_Match_Operator_Bench.matcher_matches_dynamic     avgt   60  0,176 ± 0,004  us/op
A2_Exact_Match_Operator_Bench.matcher_matches_static      avgt   60  0,145 ± 0,003  us/op
----

==== Groovy 3.0.3-indy and OpenJDK 11.0.6

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A2_Exact_Match_Operator_Bench.match_operator_dynamic      avgt   60  0,310 ± 0,008  us/op
A2_Exact_Match_Operator_Bench.match_operator_static       avgt   60  0,280 ± 0,001  us/op
A2_Exact_Match_Operator_Bench.matcher_matches_dynamic     avgt   60  0,229 ± 0,005  us/op
A2_Exact_Match_Operator_Bench.matcher_matches_static      avgt   60  0,212 ± 0,004  us/op
----

And here is the chart.

[.text-center]
--
[.img-responsive.img-thumbnail]
[link=/images/groovy-regexp-chart-02.png]
image::/images/groovy-regexp-chart-02.png[]
--

Thoughts?

* No significant differences. It looks like OpenJDK 11.0.6 generally did a bit slower, but the difference is not significant.

=== Benchmark 3: `replaceFirst` with regular expression

And here is the final benchmark.
This time we will check how `String.replaceFirst()` with pattern and closure performs compared to a conventional approach.
We can expect to see some differences between both alternatives.
The `replaceFirst()` variant has to generate a closure in memory, so it comes with some tiny overhead.
Let's see if it is significant or not.

[source,groovy]
----
package bench

import groovy.transform.CompileStatic
import org.openjdk.jmh.annotations.Benchmark
import org.openjdk.jmh.annotations.Scope
import org.openjdk.jmh.annotations.State

import java.util.regex.Matcher
import java.util.regex.Pattern

@State(Scope.Benchmark)
class A3_Regexp_Replace_Bench {

    private static final Random random = new Random()

    private static final List<String> data = [
            "v3.4.23",
            "v3.4.24",
            "v3.4.25",
            "v3.4.26",
            "v3.5.0",
            "v3.5.1",
            "v3.5.2",
            "v3.5.3",
            "v3.5.4",
            "v3.5.5",
            "v4.0.0",
            "v4.0.1",
            "v4.0.2",
            "v4.0.3",
            "v4.0.4",
            "v4.1.0",
            "v4.1.1",
            "v4.1.2",
            "v4.1.3"
    ]

    private static final Pattern pattern = ~/^v(\d{1,3})\.(\d{1,3})\.\d{1,4}$/

    private static String getVersion() {
        return data.get(random.nextInt(data.size()))
    }

    @Benchmark
    String string_replace_first_dynamic() {
        return version.replaceFirst(pattern) { _,major,minor -> "v${major}.${(minor as int) + 1}.0"}
    }

    @Benchmark
    @CompileStatic
    String string_replace_first_static() {
        return version.replaceFirst(pattern) { _,major,minor -> "v${major}.${(minor as int) + 1}.0"}
    }

    @Benchmark
    String matcher_matches_use_case_dynamic() {
        final Matcher matcher = pattern.matcher(version)
        if (!matcher.matches()) {
            throw new IllegalStateException("Pattern didn't match!")
        }

        def major = matcher.group(1)
        def minor = matcher.group(2)

        return "v${major}.${(minor as int) + 1}.0".toString()
    }

    @Benchmark
    @CompileStatic
    String matcher_matches_use_case_static() {
        final Matcher matcher = pattern.matcher(version)
        if (!matcher.matches()) {
            throw new IllegalStateException("Pattern didn't match!")
        }

        def major = matcher.group(1)
        def minor = matcher.group(2)

        return "v${major}.${(minor as int) + 1}.0".toString()
    }
}
----

Here are the results.

==== Groovy 2.5.11-indy and OpenJDK 1.8.0_242

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A3_Regexp_Replace_Bench.matcher_matches_use_case_dynamic  avgt   60  0,570 ± 0,009  us/op
A3_Regexp_Replace_Bench.matcher_matches_use_case_static   avgt   60  0,488 ± 0,008  us/op
A3_Regexp_Replace_Bench.string_replace_first_dynamic      avgt   60  0,873 ± 0,014  us/op
A3_Regexp_Replace_Bench.string_replace_first_static       avgt   60  0,832 ± 0,013  us/op
----

==== Groovy 2.5.11-indy and OpenJDK 11.0.6

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A3_Regexp_Replace_Bench.matcher_matches_use_case_dynamic  avgt   60  0,579 ± 0,015  us/op
A3_Regexp_Replace_Bench.matcher_matches_use_case_static   avgt   60  0,543 ± 0,002  us/op
A3_Regexp_Replace_Bench.string_replace_first_dynamic      avgt   60  1,034 ± 0,034  us/op
A3_Regexp_Replace_Bench.string_replace_first_static       avgt   60  0,970 ± 0,023  us/op
----

==== Groovy 3.0.3-indy and OpenJDK 1.8.0_242

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A3_Regexp_Replace_Bench.matcher_matches_use_case_dynamic  avgt   60  0,574 ± 0,011  us/op
A3_Regexp_Replace_Bench.matcher_matches_use_case_static   avgt   60  0,516 ± 0,008  us/op
A3_Regexp_Replace_Bench.string_replace_first_dynamic      avgt   60  0,925 ± 0,016  us/op
A3_Regexp_Replace_Bench.string_replace_first_static       avgt   60  0,878 ± 0,015  us/op
----

==== Groovy 3.0.3-indy and OpenJDK 11.0.6

[source,text]
----
Benchmark                                                 Mode  Cnt  Score   Error  Units
A3_Regexp_Replace_Bench.matcher_matches_use_case_dynamic  avgt   60  0,609 ± 0,013  us/op
A3_Regexp_Replace_Bench.matcher_matches_use_case_static   avgt   60  0,552 ± 0,013  us/op
A3_Regexp_Replace_Bench.string_replace_first_dynamic      avgt   60  1,014 ± 0,028  us/op
A3_Regexp_Replace_Bench.string_replace_first_static       avgt   60  0,945 ± 0,026  us/op
----

And here is the last chart.

[.text-center]
--
[.img-responsive.img-thumbnail]
[link=/images/groovy-regexp-chart-03.png]
image::/images/groovy-regexp-chart-03.png[]
--

Thoughts?

* The variant with `String.replaceFirst()` performs "slower" as expected.
* Does it make a huge difference?
I wouldn't say so.
In the worst-case scenario, using `String.replaceFirst()` with a closure like the one shown in the test will need `0.001 ms` instead of `0.0005 ms` to finish execution.

=== Conclusion

I hope those benchmarks have shown you that there are some small differences between the different variants. Still, they are not something you should be worried about.
Measuring application performance is not trivial, and in most cases, real bottlenecks exist in entirely different areas.
I can tell you from my experience that one of the first places worth checking when tweaking application's performance is its I/O layer.
I've seen countless times inefficient queries to the database, or threads blocked for nothing, and those are the real issues.

include::source/_parts/thank-you.adoc[]